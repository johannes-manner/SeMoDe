plugins {
    id 'java'
    id 'org.springframework.boot' version '2.4.1'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
}

group = 'de.uniba.dsg.serverless'
version = '0.4'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

configurations {
    umlDoclet
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    awsSdkVersion = '1.11.935'
    awsJavaCore = '1.2.1'
}

dependencies {

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // spring
    // html templating engine
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    // basic starter including tomcat as an embedded web server
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // compile time only - generating setters, getters, other utility methods
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

//    implementation 'com.h2database:h2'
    implementation 'org.postgresql:postgresql'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'

    // testing
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'

    // for java doc generation
    umlDoclet "nl.talsmasoftware:umldoclet:2.0.8"

    // aws
    compile "com.amazonaws:aws-lambda-java-core:$awsJavaCore"

    compile "com.amazonaws:aws-java-sdk-iam:$awsSdkVersion"
    compile "com.amazonaws:aws-java-sdk-cloudwatch:$awsSdkVersion"
    compile "com.amazonaws:aws-java-sdk-logs:$awsSdkVersion"
    compile "com.amazonaws:aws-java-sdk-lambda:$awsSdkVersion"
    compile "com.amazonaws:aws-java-sdk-s3:$awsSdkVersion"
    compile "com.amazonaws:aws-java-sdk-api-gateway:$awsSdkVersion"

    // azure - signature problems - see jar section
    compile group: 'com.microsoft.azure', name: 'azure-core', version: '0.9.8'
    compile group: 'com.microsoft.azure', name: 'azure-storage', version: '8.4.0'

    // google
    compile 'com.google.cloud:google-cloud-logging:1.89.0'

    // https://mvnrepository.com/artifact/com.github.docker-java/docker-java
    compile group: 'com.github.docker-java', name: 'docker-java', version: '3.1.2'

    // jersey client
    compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.29'
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.29'
    compile group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '2.29'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-math3
    // for computing regression
    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
    compile group: 'org.apache.commons', name: 'commons-csv', version: '1.6'

    // https://mvnrepository.com/artifact/commons-io/commons-io
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
}

jar {
    duplicatesStrategy = 'exclude'
    manifest {
        attributes(
                'Main-Class': 'de.uniba.dsg.serverless.ArgumentProcessor'
        )
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }

    // azure lib causes a java.lang.SecurityException: Invalid signature file digest for Manifest main attributes
    // see discussion: https://stackoverflow.com/questions/999489/invalid-signature-file-when-attempting-to-run-a-jar
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

javadoc {
    source = sourceSets.main.allJava
    options.memberLevel = JavadocMemberLevel.PRIVATE
    options.docletpath = configurations.umlDoclet.files.asType(List)
    options.doclet = "nl.talsmasoftware.umldoclet.UMLDoclet"
}